plugins {
    id 'java-library'
    id 'maven-publish'
    id "io.freefair.lombok" version "6.6.1"
    id 'signing'
}
group = "io.github.gstsgy"
version = "0.0.1"
sourceCompatibility = '17'

repositories {
    maven {
        url 'https://maven.aliyun.com/repository/public'
    }
    mavenCentral()
}
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}
task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}
dependencies {
    implementation 'org.springframework.boot:spring-boot-starter:3.0.5'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.0.5'
    api(project(':base'))
    compileOnly 'org.projectlombok:lombok:1.18.22'
    implementation('com.baomidou:mybatis-plus-boot-starter:3.5.3.1'){
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter'
        exclude group: 'org.springframework.boot', module: 'spring-boot-autoconfigure'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
//publishing {
//    publications {
//        mavenJava(MavenPublication) {
//            artifactId = 'sequence'
//            from components.java
//        }
//    }
//    repositories {
//        maven {
//            name 'wmsRepo'
//            url 'https://packages.aliyun.com/maven/repository/2073455-release-bTtGJA/'
//            credentials {
//                username '601a3e6c4fb2132a19a0f68f'
//                password 'JSdchNt9]wJZ'
//            }
//            authentication {
//                basic(BasicAuthentication)
//                digest(DigestAuthentication)
//            }
//        }
//    }
//}
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = 'sequence'
            version = project.version
            // groupId,artifactId,version，如果不定义，则会按照以上默认值执行
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'sequence'
                description = 'sequence 获取序列'
                url = 'https://github.com/gstsgy/excel-help'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'sequence'
                        name = 'guyue'
                        email = '122759654@qq.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/gstsgy/excel-help.git'
                    developerConnection = 'scm:git:git@github.com:gstsgy/excel-help.git'
                    url = 'https://github.com/gstsgy/excel-help'
                }
            }
        }
    }
    // 定义发布到哪里
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                // 这里就是之前在issues.sonatype.org注册的账号
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
javadoc {
    options.encoding = "UTF-8"
    failOnError = false
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}